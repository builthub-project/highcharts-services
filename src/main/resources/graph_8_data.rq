PREFIX cbhsv: <http://data.builthub.eu/ontology/cbhsv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX nuts: <http://data.europa.eu/nuts/>

SELECT DISTINCT ?locationName ((?value1 * 100000 ) / ?value2 AS ?Final_energy)
WHERE
{
  {
    SELECT ?locationName ?value1 WHERE {
      ?s a cbhsv:Dataset014;
        dct:temporal/dcat:startDate "2019-01-01"^^xsd:date;
        cbhsv:hasNUTS/nuts:level "0.0"^^xsd:decimal ;
        dcat:spatial/skos:prefLabel ?locationName ;
        cbhsv:measurementValue ?value1.

          FILTER (lang(?locationName) = 'en')
    }
  }
  {
    SELECT ?locationName ?value2 WHERE {
      ?s a cbhsv:Dataset026;
        dct:temporal/dcat:startDate "2019-01-01"^^xsd:date;
        cbhsv:measuredElement <http://purl.org/linked-data/sdmx/2009/code#sex-T>;
        cbhsv:hasNUTS/nuts:level "0.0"^^xsd:decimal ;
        dcat:spatial/skos:prefLabel ?locationName ;
        cbhsv:measurementValue ?value2.

          FILTER (lang(?locationName) = 'en')
    }
  }
}
ORDER BY (?locationName)
LIMIT 30